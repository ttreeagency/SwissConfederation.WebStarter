prototype(WebStarter:Default.Document) < prototype(Page) {
    head {
        xUaCompatibleMetaTag = Fusion:Tag {
            @position = 'after characterSetMetaTag'
            tagName = 'meta'
            attributes {
                http-equiv = 'x-ua-compatible'
                content = 'ie=edge'
            }
        }

        viewportMetaTag = Fusion:Tag {
            @position = 'after xUaCompatibleMetaTag'
            tagName = 'meta'
            attributes {
                content = 'width=device-width, initial-scale=1'
            }
        }

        stylesheets.base = Fusion:Array {
            main = Ttree.StyleSheet:Resource {
                uri = 'resource://Ttree.SwissConfederation.WebStarter/Public/StyleGuide/css/vendors.css'
            }
            admin = Ttree.StyleSheet:Resource {
                uri = 'resource://Ttree.SwissConfederation.WebStarter/Public/StyleGuide/css/admin.css'
            }
            print = Ttree.StyleSheet:Resource {
                uri = 'resource://Ttree.SwissConfederation.WebStarter/Public/StyleGuide/css/print.css'
            }
            frontend = Ttree.StyleSheet:Resource {
                uri = 'resource://Ttree.SwissConfederation.WebStarter/Public/Styles/Frontend.css'
            }
            backend = Ttree.StyleSheet:Resource {
                uri = 'resource://Ttree.SwissConfederation.WebStarter/Public/Styles/Backend.css'
                @if.onlyRenderWhenNotInLiveWorkspace = ${documentNode.context.inBackend}
            }
        }

        stylesheets.site = Fusion:Template {
            templatePath = 'resource://Ttree.SwissConfederation.WebStarter/Private/Fusion/Document/Default/Document.html'
            sectionName = 'stylesheets'
        }

        msapplicationMetaTag = Fusion:Array {
            @position = 'after stylesheets'

            tileColor = Fusion:Tag {
                tagName = 'meta'
                attributes {
                    name = 'msapplication-TileColor'
                    content = '#ffffff'
                }
            }

            tileImage = Fusion:Tag {
                tagName = 'meta'
                attributes {
                    name = 'msapplication-TileImage'
                    content = Fusion:ResourceUri {
                        path = 'resource://Ttree.SwissConfederation.WebStarter/Public/StyleGuide/img/ico/mstile-144x144.png'
                    }
                }
            }

            config = Fusion:Tag {
                tagName = 'meta'
                attributes {
                    name = 'msapplication-config'
                    content = Fusion:ResourceUri {
                        path = 'resource://Ttree.SwissConfederation.WebStarter/Public/StyleGuide/img/ico/browserconfig.xml'
                    }
                }
            }
        }


        javascripts.site = Fusion:Template {
            templatePath = 'resource://Ttree.SwissConfederation.WebStarter/Private/Fusion/Document/Default/Document.html'
            sectionName = 'headScripts'
        }
    }

    bodyTag {
        attributes {
            class = 'mod mod-layout'
        }
    }

    body {
        templatePath = 'resource://Ttree.SwissConfederation.WebStarter/Private/Fusion/Document/Default/Document.html'
        sectionName = 'body'
        parts = Fusion:Array {
            nav = WebStarter:Header.AccessibilityNavigation

            main = WebStarter:Container.DocumentWrapper {
                content {
                    header = WebStarter:Header.Base {
                        title = ${q(site).property('title')}

                        topNav {
                            treecrumb = WebStarter:Header.Treecrumb
                            languageSelect = WebStarter:Header.LanguageSelection
                            serviceNav = WebStarter:Header.ServiceNavigation
                        }

#                        section {
#                            siteSearch = WebStarter:Header.SiteSearch
#                            topic = WebStarter:Header.Topic
#                        }
                    }

                    globalNavigation = WebStarter:Container.GlobalNavigation

                    breadcrumb = WebStarter:Container.Breadcrumb

                    main = WebStarter:Container.ContentArea
                }
            }
        }
        javascripts.site = Fusion:Template {
            templatePath = 'resource://Ttree.SwissConfederation.WebStarter/Private/Fusion/Document/Default/Document.html'
            sectionName = 'bodyScripts'
        }

        dynamicStyles = Ttree.Fusion.DynamicStyles:InlineStyles
        @process.dynamicStyles = ${this.dynamicStyles + value}
    }
}
